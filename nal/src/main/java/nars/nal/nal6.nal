//// implication-based syllogism ////////////////////////////////////////////////////////////////////////////////////
//       (A ==> B) ------- (B ==> C)
//            \               /
//             \             /
//              \           /
//               \         /
//                (A ==> C)
//If after S M happens, and after M P happens, so P happens after S

    //"Please note that the temporal order in the conclusion of a weak rule is
    //not necessarily the same as the order in the premises, given the hypothetical
    //nature of such a rule."

    (M ==> P), (S ==> M), neqCom(P,S), notImplEqui(S), notEqui(P) |- (S ==>+- P), (Belief:Deduction)
        //(S ==> M), (M ==> P), neqCom(P,S), notImplEqui(S), notEqui(P) |- (S ==>+- P), (Belief:Deduction)

    (P ==> M), (S ==> M), neqCom(P,S), notImplEqui(S), notEqui(P) |- (S ==>+- P), (Belief:Induction)
        //TODO unit test:
        //(S ==> P), (S ==> M), task("."), neqCom(P,S), notImplEqui(S), notEqui(P) |- (P ==>+- M), (Punctuation:Question)

    (M ==> P), (M ==> S), neqCom(P,S), notImplEqui(S), notEqui(P) |- (S ==>+- P), (Belief:Abduction)

    (P ==> M), (M ==> S), neqCom(P,S), notImplEqui(S), notEqui(P) |- (S ==>+- P), (Belief:Exemplification)


//// implication to equivalence ////////////////////////////////////////////////////////////////////////////////////
//If when S happens, P happens, and before P happens, S has happened, then they are truth-related equivalent

    (S ==> P), (P ==> S), notImplEqui(S), notImplEqui(P) |- (P <=>+- S), (Belief:Intersection)

//// equivalence-based syllogism ////////////////////////////////////////////////////////////////////////////////////
//Same as for inheritance again
    //TODO some of these notEqui and notImplEqui can be removed because the premise is already valid in the same way as the conclusion would be

    (M ==> P), (M ==> S), notImplEqui(S), notImplEqui(P), neqCom(P,S) |- (P <=>+- S), (Belief:Comparison)

    (P ==> M), (S ==> M), notImplEqui(S), notImplEqui(P), neqCom(P,S) |- (P <=>+- S), (Belief:Comparison)

    (M <=> P), (S <=> M), notImplEqui(S), notImplEqui(P), neqCom(P,S) |- (P <=>+- S), (Belief:Resemblance)

    (M ==> P), (S <=> M), notImplEqui(S), notEqui(P), neqCom(P,S) |- (S ==>+- P), (Belief:Analogy)

    (P ==> M), (S <=> M), notImplEqui(P), notEqui(S), neqCom(P,S) |- (P ==>+- S), (Belief:Analogy)


// Contraposition (structural)
// "If not smoking lets you be healthy, being not healthy may be the result of smoking"
// original:	    ( --S ==> P),   P |- ( --P ==> S), (Truth:Contraposition, Derive:AllowBackward)
// original:	    ( --S ==> P), --S |- ( --P ==> S), (Truth:Contraposition, Derive:AllowBackward)

    ( (--,%S) ==> P), ( (--,%S) ==> P) |- ( (--,%P) ==>+- S),       (Belief:Contraposition)


//// implication-based composition ////////////////////////////////////////////////////////////////////////////////////
//Same as for inheritance
    //Note: the P && S subterm of the conclusion may get rewritten with appropraite 'dt' as per dtCombinePre/dtCombinePost.
    //otherwise it will remain DTERNAL. using XTERNAL (&&+-) here is more trouble than its worth

    (P ==> M), (S ==> M), neqRCom(S,P) |- ((P &&+- S) ==>+- M), (Belief:Intersection)
        //(P ==> M), (S ==> M), neqCom(P,S) |- ((%P || %S) ==>+- M), (Belief:Intersection)

    (M ==> P), (M ==> S), neqRCom(S,P) |- (M ==>+- (P &&+- S)), (Belief:Intersection)
        //(M ==> P), (M ==> S), neqCom(P,S) |- (M ==>+- (%P || %S)), (Belief:Union)


//// implication-based decomposition ////////////////////////////////////////////////////////////////////////////////////
//Same as for inheritance again


    (S ==> M), ((&&,S,A..+) ==> M) |- ((&&,A..+) ==>+- M), (Belief:DecomposeNegativePositivePositive)
        //(S ==> M), ((&&,(--,%S),A..+) ==> M) |- ((&&,A..+) ==>+- M), (Belief:DecomposeNegativePositivePositiveN)
        //(%S ==> %M), ((||,%S,A..+) ==> M) |- ((||,A..+) ==>+- %M), (Belief:DecomposePositiveNegativeNegative)

    (M ==> S), (M ==> (&&,S,A..+)) |- (M ==>+- (&&,A..+)), (Belief:DecomposePositiveNegativeNegative)
        (M ==> S), (M ==> (&&,(--,%S),A..+)) |- (M ==>+- (&&,A..+)), (Belief:DecomposePositiveNegativeNegativeN)
        //(%M ==> %S), (%M ==> (||,%S,A..+)) |- (%M ==>+- (||,A..+)), (Belief:DecomposeNegativePositivePositive)

    (M ==> C), C, belief("&&") |- (M ==>+- dropAnyConj(C)), (Belief:StructuralDeduction)
        ((--,%M) ==> C), C, belief("&&") |- ((--,%M) ==>+- dropAnyConj(C)), (Belief:StructuralDeduction)
        //(M ==> (&&,(--,%S),A..+)), M |- (M ==>+- (&&,A..+)), (Belief:StructuralDeduction)
    (C ==> M), C, belief("&&") |- (dropAnyConj(C) ==>+- M), (Belief:StructuralDeduction)
        (C ==> (--,%M)), C, belief("&&") |- (dropAnyConj(C) ==>+- (--,%M)), (Belief:StructuralDeduction)
        //((&&,(--,%S),A..+) ==> M), M |- ((&&,A..+) ==>+- M), (Belief:StructuralDeduction)

//// conditional syllogism ////////////////////////////////////////////////////////////////////////////////////
//      + Indep variable elimination
//If after M, P usually happens, and M happens, it means P is expected to happen

// NAL7 specific inference ////////////////////////////////////////////////////////////////////////////////////
//      Reasoning about temporal statements. those are using the ==> relation because relation in time is a relation of the truth between statements.


    B, (A ==> C), belief(positive), notImplEqui(B) |- subIfUnifiesAny(C,A,B), (Belief:Deduction, Goal:Induction)
    B, ((--,%A) ==> C), belief(positive), notImplEqui(B) |- subIfUnifiesAny((--,%C),A,B), (Belief:Deduction, Goal:Induction)
        B, (A ==> C), belief(negative), notImplEqui(B) |- subIfUnifiesAny((--,%C),A,B), (Belief:DeductionPN, Goal:InductionPN)
        B, ((--,%A) ==> C), belief(negative), notImplEqui(B) |- subIfUnifiesAny((--,%C),A,B), (Belief:DeductionNN, Goal:InductionNN)

    B, (C ==> A), belief(positive), notImplEqui(B) |- subIfUnifiesAny(C,A,B), (Belief:Abduction, Goal:Deduction)
    B, ((--,%C) ==> A), belief(positive), notImplEqui(B) |- subIfUnifiesAny((--,%C),A,B), (Belief:Abduction, Goal:Deduction)
        B, (C ==> A), belief(negative), notImplEqui(B) |- subIfUnifiesAny((--,%C),A,B), (Belief:AbductionPN, Goal:DeductionPN)
        B, ((--,%C) ==> A), belief(negative), notImplEqui(B) |- subIfUnifiesAny((--,%C),A,B), (Belief:AbductionNN, Goal:DeductionNN)

    // may only need 2 of these 4:
    B, (A <=> C), neq(C, B), task(positive), belief(positive) |- subIfUnifiesAny(C,A,B), (Belief:Analogy, Goal:Deduction)
    B, (A <=> C), neq(C, B), task(positive), belief(negative) |- subIfUnifiesAny((--,%C),A,B), (Belief:AnalogyPN, Goal:DeductionPN)
    B, (A <=> C), neq(C, B), task(negative), belief(positive) |- subIfUnifiesAny((--,%C),A,B), (Belief:AnalogyN, Goal:DeductionN)
    B, (A <=> C), neq(C, B), task(negative), belief(negative) |- subIfUnifiesAny(C,A,B), (Belief:AnalogyNN, Goal:DeductionNN)

    //variable elimination 4b:
    (X --> A), ((#W --> A)<->(#W --> C)),  neq(X,C), neq(X,#W) |- (X-->C), (Belief:Analogy, Goal:Deduction)



            ////desire spreading by equivalence
            //     S, (S<=>P), task(positive), belief(positive) |-      P,  (Goal:Strong)
            //     S, (S<=>P), task(positive), belief(negative) |- (--,%P), (Goal:StrongN)
            //     S, (S<=>P), task(negative), belief(positive) |- (--,%P), (Goal:StrongN)
            //     S, (S<=>P), task(negative), belief(negative) |- (--,%P), (Goal:Strong)

    //answering
    Z, (X==>Y), task("?") |- subIfUnifiesAny((X ==>+- Y), Y, Z, strict), (Belief:BeliefStructuralDeduction, Punctuation:Belief)
    Z, (X==>Y), task("?") |- subIfUnifiesAny((X ==>+- Y), X, Z, strict), (Belief:BeliefStructuralAbduction, Punctuation:Belief)
    Z, (X<=>Y), task("?") |- subIfUnifiesAny((X <=>+- Y), Y, Z, strict), (Belief:BeliefStructuralAnalogy, Punctuation:Belief)

    //strong unification
    (Y==>X),Z,task(".")       |- subIfUnifiesAny(X,Y,Z), (Belief:Deduction)
    ((--,%Y)==>X),Z,task(".") |- subIfUnifiesAny(X,Y,Z), (Belief:DeductionPN)
    (X==>Y),Z,task(".")       |- subIfUnifiesAny(X,Y,Z), (Belief:Abduction)
    ((--,%X)==>Y),Z,task(".") |- (--,subIfUnifiesAny(X,Y,Z)), (Belief:AbductionPN)
    (X<=>Y),Z,task(".")       |- subIfUnifiesAny(X,Y,Z), (Belief:Comparison)
        //TODO check the truth here

    //see: https://en.wikipedia.org/wiki/Imperative_logic#Ross.27_Paradox
    //originally these were only valid if A occurrs during or after C. the opposite is not logical and would also produce redundant results
    B, (C && A), task("!") |- subIfUnifiesAny(C,A,B), (Goal:Strong)
    B, (C && (--,%A)), task("!") |- subIfUnifiesAny(C,A,B), (Goal:StrongN)
        //B, (C && A) |- (--,subIfUnifiesAny(C,A,B)), (Goal:StrongN)
        //B, (C && (--,%A)) |- subIfUnifiesAny(C,A,B), (Goal:StrongN)



//// multi-conditional syllogism ////////////////////////////////////////////////////////////////////////////////////
//Inference about the pre/postconditions

    Y, ((&&,X,A..+) ==> B), task(".") |- subIfUnifiesAny(((&&,A..+) ==>+- B),X,Y), (Belief:Deduction)
    Y, ((&&,(--,%X),A..+) ==> B), task(".") |- subIfUnifiesAny(((&&,A..+) ==>+- B),X,Y), (Belief:DeductionN)

    Y, (B ==> (&&,X,A..+)), task(".") |- subIfUnifiesAny((B ==>+- (&&,A..+)),X,Y), (Belief:Deduction)
    Y, (B ==> (&&,(--,%X),A..+)), task(".") |- subIfUnifiesAny((B ==>+- (&&,A..+)),X,Y), (Belief:DeductionN)

//// propositional decomposition ////////////////////////////////////////////////////////////////////////////////////
//If S is the case, and (&&,S,A..+) is not the case, it can't be that (&&,A..+) is the case

    S, (&&,S,A..+), task(".") |- (&&,A..+), (Belief:DecomposePositiveNegativeNegative)
        //S, (||,S,A..+) |- (||,A..+), (Belief:DecomposeNegativePositivePositive)
    S, (&&,(--,%S),A..+), task(".") |- (&&,A..+), (Belief:DecomposeNegativeNegativeNegative)
        //S, (||,(--,%S),A..+) |- (||,A..+), (Belief:DecomposePositivePositivePositive)


//conditional abduction TODO this needs to be 'dt' sensitive

    ((&&,M,X,A..*) ==> C), (Y ==> C), task(".") |- subIfUnifiesAny(M,X,Y), (Belief:Abduction)
    ((&&,M,A..+) ==> C), ((&&,A..+) ==> C), task(".") |- M, (Belief:Abduction)

        //TODO modify ellipsis to allow these two rules to be expressed as one:
        // ((&&,M,A..+) ==> C), (A..+ ==> C) |- M, (Belief:Abduction)
        //  which will match A..+ to a Conjunction


//Can be derived by NAL7 rules so this won't be necessary there (Order:ForAllSame left out here)

//the first rule does not have Order:ForAllSame because it would be invalid, see: https://groups.google.com/forum/#!topic/open-nars/r5UJo64Qhrk
    //((&&,A..+) ==> C), M, neqRCom(C,M), notImplEqui(M) |- ((&&,M,A..+) ==>+- C), (Belief:Induction)

    ((&&,M,A..+) ==> C), (B ==> M), neqRCom(C,B)  |- ((&&,B,A..+) ==>+- C), (Belief:Deduction)
    ((&&,B,A..+) ==> C), (B ==> M), neqRCom(C,M) |- ((&&,M,A..+) ==>+- C), (Belief:Abduction)

    ((&&,M,A..+) ==> C), ((&&,B,A..+) ==> C), neqRCom(B,M), notEqui(M), notImplEqui(B) |- (B ==>+- M), (Belief:Induction)



//conditional abduction by dependent variable

    ((X --> R) ==> Z), ((&&,(#Y --> B),(#Y --> R),A..*) ==> Z)  |- (X --> B), (Belief:Abduction)
        ((X --> R) ==> Z), ((&&,(#Y --> B),--(#Y --> R),A..*) ==> Z) |- (X --> B), (Belief:AbductionN)

// conditional deduction "An inverse inference has been implemented as a form of deduction" https://code.google.com/p/open-nars/issues/detail?id=40&can=1

    (U --> L), ((&&,(#X --> L),(#X --> R),A..*) ==> Z) |- substitute(((&&,A..*,(U --> R)) ==>+- Z),#X,U,strict), (Belief:Deduction)
        (U --> L), ((&&,--(#X --> L),(#X --> R),A..*) ==> Z) |- substitute(((&&,A..*,(U --> R)) ==>+- Z),#X,U,strict), (Belief:DeductionN)



//// second level variable handling rules ////////////////////////////////////////////////////////////////////////////////////
//second level variable elimination (termlink level2 growth needed in order for these rules to work)

    (B --> K), (&&,(#X --> L),(($Y --> K) ==> A)) |- substitute((&&, (#X --> L), A), $Y, B, strict), (Belief:Deduction)
        (B --> K), (&&,(#X --> L),((--,($Y --> K)) ==> A)) |- substitute((&&, (#X --> L), A), $Y, B, strict), (Belief:DeductionN)
    (B --> K), (($X --> L) ==> (&&,(#Y --> K),A..+)) |- substitute((($X --> L) ==>+- (&&,A..+)),#Y,B, strict), (Belief:AnonymousAnalogy)
        (B --> K), (($X --> L) ==> (&&,--(#Y --> K),A..+)) |- substitute((($X --> L) ==>+- (&&,A..+)),#Y,B, strict), (Belief:AnonymousAnalogyN)


//precondition combiner inference rule (variable_unification6):
//  TODO can these also apply to goals somehow?
//  TODO negative versions

//    ((&&,C,A..+) ==> Z), ((&&,C,B..+) ==> Z), neq(A..+,B..+) |- ((&&,A..+) ==>+- (&&,B..+)), (Belief:Induction)
//    ((&&,C,A..+) ==> Z), ((&&,C,B..+) ==> Z), neq(A..+,B..+) |- ((&&,B..+) ==>+- (&&,A..+)), (Belief:Induction)
//    (Z ==> (&&,C,A..+)), (Z ==> (&&,C,B..+)), neq(A..+,B..+) |- ((&&,A..+) ==>+- (&&,B..+)), (Belief:Abduction)
//    (Z ==> (&&,C,A..+)), (Z ==> (&&,C,B..+)), neq(A..+,B..+) |- ((&&,B..+) ==>+- (&&,A..+)), (Belief:Abduction)




//// conjunction decompose AND dependent variable elimination - decomposition with elimination of a variable

     //structural decompose any component of a conjunction.
     //     NOTE: the multi-term decomposition allows the superset of subterms without overlap. if they are chosen only one at a time, then overlap will prevent their recombination to the aforementioned superset result so it is incomplete. thus only the ellipsis form is necessary

     C, X, task("&&") |- dropAnyEvent(C), (Belief:StructuralDeduction, Goal:StructuralDeduction)

     (&&,X,A..+), Y, task(".") |- subIfUnifiesAny((&&,A..+),X,Y,strict), (Belief:AnonymousAnalogy)
     (&&,(--,%X),A..+), Y, task(".") |- (--,subIfUnifiesAny((&&,A..+),X,Y,strict)), (Belief:AnonymousAnalogy)

        //TODO add a conj() guard to fast fail
        //(&&,(--,%X),A..+), X |- (&&,A..+), (Belief:StructuralDeduction, Goal:StructuralDeduction)


     ((&&,X,A..+) && Z), Z |- (X &&+- Z), (Belief:StructuralDeduction, Goal:StructuralDeduction)
     (Z && (&&,X,A..+)), Z |- (Z &&+- X), (Belief:StructuralDeduction, Goal:StructuralDeduction)
        ((&&,X,A..+) && (--,%Z)), Z |- (X &&+- (--,%Z)), (Belief:StructuralDeduction, Goal:StructuralDeduction)
        ((--,%Z) && (&&,X,A..+)), Z |- ((--,%Z) &&+- X), (Belief:StructuralDeduction, Goal:StructuralDeduction)

     (&&,X,A..+), B, task("!") |- subIfUnifiesAny((&&,A..+),X,B), (Goal:Strong)
        (&&,(--,%X),A..+), B,  task("!") |- subIfUnifiesAny((&&,A..+),X,B), (Goal:StrongPN)

     //X, (&&,X,A..+),            task("?@") |- (&&,A..+), ()
     //   X, (&&,(--,%X),A..+),   task("?@") |- (&&,A..+), ()
     //B, (A==>B),                task("?@") |- A, ()
     //A, (A==>B),                task("?@") |- B, ()
     //A, ((--,%A)==>B),          task("?@") |- B, ()

     //quest to 'how' question
     //B, X, neqRCom(B,X), task("@"), notImplEqui(B), notEqui(X) |- (B ==>+- X), (Punctuation:Question)
     //B, X, neqRCom(B,X), task("@"), notImplEqui(B), notEqui(X) |- ((--,%B) ==>+- X), (Punctuation:Question)

     //(&&,X,A..+), X, task("?@") |- (&&,A..+), ()
     //   (&&,(--,%X),A..+), X, task("?@") |- (&&,A..+), ()


     //B, C, belief("&&") |- subIfUnifiesDep(dropAnyConj(C),X,B), (Belief:AnonymousAnalogy)
                //,Goal:Deduction
                //Goal:Goduction)

///// conjunction decompose
//
//    (&&,A_1..n), A_1 |- A_1, (Truth:StructuralDeduction, Desire:StructuralStrong)
//    (&/,A_1..n), A_1 |- A_1, (Truth:StructuralDeduction, Desire:StructuralStrong)
//    (&|,A_1..n), A_1 |- A_1, (Truth:StructuralDeduction, Desire:StructuralStrong)
//    (&/,B,A_1..n), B, task("!") |- (&/,A_1..n), (Truth:Deduction, Desire:Strong, SequenceIntervals:FromPremises)
//
////// propositional decomposition ////////////////////////////////////////////////////////////////////////////////////
////If S is the case, and (&&,S,A_1..n) is not the case, it can't be that (&&,A_1..n) is the case
//
//    S, (&/,S,A_1..n) |- (&/,A_1..n), (Truth:DecomposePositiveNegativeNegative, SequenceIntervals:FromPremises)
//    S, (&|,S,A_1..n) |- (&|,A_1..n), (Truth:DecomposePositiveNegativeNegative)
//    S, (&&,S,A_1..n) |- (&&,A_1..n), (Truth:DecomposePositiveNegativeNegative)
//    S, (||,S,A_1..n) |- (||,A_1..n), (Truth:DecomposeNegativePositivePositive)
//
////Additional for negation: https://groups.google.com/forum/#!topic/open-nars/g-7r0jjq2Vc
//
//    S, (&/,(--,S),A_1..n) |- (&/,A_1..n), (Truth:DecomposeNegativeNegativeNegative, SequenceIntervals:FromPremises)
//    S, (&|,(--,S),A_1..n) |- (&|,A_1..n), (Truth:DecomposeNegativeNegativeNegative)
//    S, (&&,(--,S),A_1..n) |- (&&,A_1..n), (Truth:DecomposeNegativeNegativeNegative)
//    S, (||,(--,S),A_1..n) |- (||,A_1..n), (Truth:DecomposePositivePositivePositive)

