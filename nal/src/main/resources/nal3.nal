//// inheritance composition ////////////////////////////////////////////////////////////////////////////////////
//If P and S are in the intension/extension of M, then union/difference and intersection can be built:
//The duality of extension and intension in NAL corresponds to the
//  duality of intersection and union in set theory â€” intensional intersection
//  corresponds to extensional union, and extensional intersection corresponds
//  to intensional union.

    (P --> M), (S --> M), task("."), notSet(S), notSet(P), neqRCom(S,P) |- ((S | P) --> M), (Belief:Intersection)
    (P --> M), (S --> M), task("."), notSet(S), notSet(P), neqRCom(S,P) |- ((S & P) --> M), (Belief:Union)
    (P --> M), (S --> M), task("."), notSet(S), notSet(P), neqRCom(S,P) |- ((P ~ S) --> M), (Belief:Difference)

    (M --> P), (M --> S), task("."), notSet(S), notSet(P), neqRCom(S,P) |- (M --> (P & S)), (Belief:Intersection)
    (M --> P), (M --> S), task("."), notSet(S), notSet(P), neqRCom(S,P) |- (M --> (P | S)), (Belief:Union)
    (M --> P), (M --> S), task("."), notSet(S), notSet(P), neqRCom(S,P) |- (M --> (P - S)), (Belief:Difference)

//raw product composition, may be redundant because general induction via && does the equivalent
 //    A, B, task("*"), belief("*") |- (A | B), (Belief:Intersection)
 //   A, B, task("*"), belief("*") |- (A & B), (Belief:Union)
    A, B, task("*"), belief("*"), neqRCom(A,B) |- (A - B), (Belief:Difference)

//raw product decomposition ?

//// inheritance decomposition ////////////////////////////////////////////////////////////////////////////////////
//if (S --> M) is the case, and ((|,S,A..+) --> M) is not the case, then ((|,A..+) --> M) is not the case, hence Belief:DecomposePositiveNegativeNegative

     (X --> M), ((|,X,A..+) --> M) |- ((|,A..+) --> M), (Belief:DecomposePositiveNegativeNegative, Goal:DecomposePositiveNegativeNegative)
     (X --> M), ((&,X,A..+) --> M) |- ((&,A..+) --> M), (Belief:DecomposeNegativePositivePositive, Goal:DecomposeNegativePositivePositive)

     (M --> X), (M --> (&,X,A..+)) |- (M --> (&,A..+)), (Belief:DecomposePositiveNegativeNegative, Goal:DecomposePositiveNegativeNegative)
     (M --> X), (M --> (|,X,A..+)) |- (M --> (|,A..+)), (Belief:DecomposeNegativePositivePositive, Goal:DecomposeNegativePositivePositive)

     (S --> M), ((S ~ P) --> M) |- (P --> M), (Belief:DecomposePositiveNegativePositive, Goal:DecomposePositiveNegativePositive)
     (S --> M), ((P ~ S) --> M) |- (P --> M), (Belief:DecomposeNegativeNegativeNegative, Goal:DecomposeNegativeNegativeNegative)

     (M --> S), (M --> (S - P)) |- (M --> P), (Belief:DecomposePositiveNegativePositive, Goal:DecomposePositiveNegativePositive)
         S, (S - P) |- P, (Belief:DecomposePositiveNegativePositive, Goal:DecomposePositiveNegativePositive)

     (M --> S), (M --> (P - S)) |- (M --> P), (Belief:DecomposeNegativeNegativeNegative, Goal:DecomposeNegativeNegativeNegative)
         S, (P - S) |- P, (Belief:DecomposeNegativeNegativeNegative, Goal:DecomposeNegativeNegativeNegative)


//NAL3 single premise decompose

    ((|,X,A..+) --> M), X |- ((|,A..+) --> M), (Belief:StructuralDeduction, Goal:StructuralDeduction)
    (M --> (&,X,A..+)), X |- (M --> (&,A..+)), (Belief:StructuralDeduction, Goal:StructuralDeduction)

    ((B ~ G) --> S), G, neqRCom(B,S) |- (B --> S), (Belief:StructuralDeduction, Goal:StructuralDeduction)
    (S --> (B - G)), G, neqRCom(B,S) |- (S --> B), (Belief:StructuralDeduction, Goal:StructuralDeduction)

////composition on both sides of a statement:

    ((&,B,A..+) --> (&,X,A..+)), (B --> X), task("?") |- ((&,B,A..+) --> (&,X,A..+)), (Belief:BeliefStructuralDeduction, Punctuation:Belief)
    ((|,B,A..+) --> (|,X,A..+)), (B --> X), task("?") |- ((|,B,A..+) --> (|,X,A..+)), (Belief:BeliefStructuralDeduction, Punctuation:Belief)

    ((-,S,A) --> (-,S,B)), (B --> A), task("?") |- ((-,S,A) --> (-,S,B)), (Belief:BeliefStructuralDeduction, Punctuation:Belief)
    ((~,S,A) --> (~,S,B)), (B --> A), task("?") |- ((~,S,A) --> (~,S,B)), (Belief:BeliefStructuralDeduction, Punctuation:Belief)

////composition on one side of a statement:

    (W --> (|,B,A..+)), (W --> B), task("?") |- (W --> (|,B,A..+)), (Belief:BeliefStructuralDeduction, Punctuation:Belief)
    ((&,B,A..+) --> W), (B --> W), task("?") |- ((&,B,A..+) --> W), (Belief:BeliefStructuralDeduction, Punctuation:Belief)

    (W --> (-,S,B)), (W --> B), task("?"), neqRCom(S,B) |- (W --> (-,S,B)), (Belief:BeliefStructuralDifference, Punctuation:Belief)
    ((~,S,B) --> W), (B --> W), task("?"), neqRCom(S,B) |- ((~,S,B) --> W), (Belief:BeliefStructuralDifference, Punctuation:Belief)



//experiment
//     (M --> (S - P)), (M --> S), task("!") |- (M --> P), (Goal:DecomposePositiveNegativeNegative)
//     (M --> (S - P)), (M --> P), task("!") |- (M --> S), (Goal:DecomposeNegativeNegativeNegative)
